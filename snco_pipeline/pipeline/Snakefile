from snco_pipeline.pipeline_utils import (
    format_command, annotations_getter, raw_data_getter, results_getter,
    fasta_getter, gtf_getter, barcode_whitelist_getter,
    conda_env_getter
)


annotation = annotations_getter(config)
raw_data = raw_data_getter(config)
results = results_getter(config)
get_fasta = fasta_getter(config)
get_gtf = gtf_getter(config)
get_barcode_whitelist = barcode_whitelist_getter(config)
get_conda_env = conda_env_getter(config)


ref_genotypes = set()
genotypes = set()
for ds in config['datasets'].values():
    ref_genotypes.add(ds['reference_genotype'])
    for geno in ds['genotypes'].values():
        genotypes.update(list(geno['founder_haplotypes'].values()))


wildcard_constraints:
    dataset_name='|'.join(config['datasets']),
    haplo='hap1|hap2',
    dataset_name_plus_optional_haplo='(?:' + '|'.join(config['datasets']) + ')(?:_hap[12])?',
    sample_name=r'\w+',
    ref='|'.join(ref_genotypes),
    qry='|'.join(genotypes),
    geno='|'.join(genotypes),


tech_types = set([ds['technology'] for ds in config['datasets'].values()])
is_plate_based = tech_types.intersection(['takara_dna', 'plate_wgs'])
is_droplet_based = tech_types.intersection(['10x_rna_v4', '10x_rna_v3', 'bd_rna', '10x_atac'])
if is_plate_based and is_droplet_based:
    raise ValueError('Cannot mix droplet and plate technologies in the same run, please separate to different configs.')


include: "rules/variants.snakefile"
if is_plate_based:
    include: "rules/align_plate.snakefile"
else:
    include: "rules/align_droplet.snakefile"
include: "rules/haplotype.snakefile"


def all_results():
    result_fns = []
    for dataset_name, ds_conf in config['datasets'].items():
        if ds_conf['ploidy'] != 'diploid_f1*f1':
            result_fns += expand(
                [results('haplotypes/{dataset_name}.{file_type}.json'),
                 results('analysis/{dataset_name}.haplotyping_report.py.ipynb')],
                dataset_name=dataset_name,
                file_type=['markers_init', 'markers_filt', 'pred'],
            )
        else:
            result_fns += expand(
                [results('haplotypes/{dataset_name}_{haplo}.{file_type}.json'),
                 results('analysis/{dataset_name}_{haplo}.haplotyping_report.py.ipynb')],
                dataset_name=dataset_name,
                haplo=['hap1', 'hap2'],
                file_type=['markers_init', 'markers_filt', 'pred'],
            )
    return result_fns


rule all:
    input:
        all_results()